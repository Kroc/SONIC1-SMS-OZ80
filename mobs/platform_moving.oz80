;; Sonic 1 Master System Disassembly
   -------------------------------------------------------------------------------------------------
   created by Kroc Camen <kroc@camendesign.com> and given to the Public Domain; you may do anything
   you like with this code as long as you respect the TradeMarks and (any) Copyrights within.
   absolutely no warranty implied
   =================================================================================================
;;

;wooden platform - moving (Green Hill)

PROC    :mob_platform_leftRight                 SECTION ::mobs                  ;$6D65
        ;==============================================================================
PARAMS  IX`mob          ``Address of the current mob being processed
{
        set     5,[ix+#mob.flags]               ;mob does not collide with the floor
        
        ld      a,[$.CURRENT_LEVEL]
        cp      $07                             ;Jungle act 2?
        jr      z,._1
        
        ld      hl,$0020
        ld      [$.D267],hl
        
        ld      hl,$0048
        ld      [$.D269],hl
        
        ld      hl,$0030
        ld      [$.D26B],hl
        
        ld      hl,$0030
        ld      [$.D26D],hl
        
._1     ld      [ix+#mob.width], 26
        ld      [ix+#mob.height],16
        ld      c,$00
        
        ld      a,[$.SONIC.Ydirection]
        and     a
        jp      m,._2
        
        ld      hl,$0806
        ld      [$.TEMP6],hl
        call    :detectCollisionWithSonic
        
        ld      c,$00
        jr      c,._2
        
        ld      bc,$0010
        ld      de,$0000
        call    :_LABEL_7CC1_12
        ld      c,$01
        
        ;move right 1px
._2     ld      l,[ix+$12]
        ld      h,[ix+#mob.unknown13]
        inc     hl
        ld      [ix+$12],l
        ld      [ix+#mob.unknown13],h
        
        ld      de,$00A0
        xor     a`zero                          ;set A to 0
        sbc     hl,de
        jr      c,._3
        
        ld      [ix+$12],a`zero
        ld      [ix+#mob.unknown13],a`zero
        inc     [ix+#mob.unknown14]
        
._3     ld      de,$0001
        bit     0,[ix+#mob.unknown14]
        jr      z,._4
        
        ;move left 1px?
        ld      de,$FFFF
._4     ld      l,[ix+#mob.X+0]
        ld      h,[ix+#mob.X+1]
        add     hl,de
        ld      [ix+#mob.X+0],l
        ld      [ix+#mob.X+1],h
        ld      a,c
        and     a
        jr      z,._5
        
        ld      hl,[$.SONIC.X]
        add     hl,de
        ld      [$.SONIC.X],hl
        
._5     ld      hl,:object_platform_spriteLayouts._6911
        ld      a,[$.LEVEL_SOLIDITY]
        and     a
        jr      z,._6
        
        ld      hl,:object_platform_spriteLayouts._6931
        dec     a
        jr      z,._6
        
        ld      hl,:object_platform_spriteLayouts._6923
._6     ld      [ix+#mob.spriteLayout+0],l
        ld      [ix+#mob.spriteLayout+1],h
        ret
}