;; Sonic 1 Master System Disassembly
   -------------------------------------------------------------------------------------------------
   created by Kroc Camen <kroc@camendesign.com> and given to the Public Domain; you may do anything
   you like with this code as long as you respect the TradeMarks and (any) Copyrights within.
   absolutely no warranty implied
   =================================================================================================
;;

PROC    :mob_meta_trip                          SECTION ::mobs                  ;$7AA7
        ;==============================================================================
PARAMS  IX`mob          ``Address of the current mob being processed
{
        set     5,[ix+#mob.flags]               ;mob does not collide with the floor
        ld      [ix+#mob.width], 64
        ld      [ix+#mob.height],64
        ld      hl,$0000
        ld      [$.TEMP6],hl
        call    :detectCollisionWithSonic
        ret     c
        bit     6,[iy+#vars.flags6]
        ret     nz
        ld      a,[$.SONIC.flags]
        and     $80
        ret     z
        ld      hl,$fffb
        xor     a`zero
        ld      [$.SONIC.Yspeed+0],a`zero
        ld      [$.SONIC.Yspeed+1],hl
        ld      hl,$0003
        xor     a`zero
        ld      [$.SONIC.Xspeed+0],a`zero
        ld      [$.SONIC.Xspeed+1],hl
        ld      hl,$.SONIC.flags
        res     1,[hl]
        set     6,[iy+#vars.flags6]
        ld      [iy+#vars.joypad],$ff
        ld      a,$11
        rst     :rst_playSFX
        ret
}