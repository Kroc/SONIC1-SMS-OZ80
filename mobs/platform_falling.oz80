;; Sonic 1 Master System Disassembly
   -------------------------------------------------------------------------------------------------
   created by Kroc Camen <kroc@camendesign.com> and given to the Public Domain; you may do anything
   you like with this code as long as you respect the TradeMarks and (any) Copyrights within.
   absolutely no warranty implied
   =================================================================================================
;;

;wooden platform - falling (Green Hill)

PROC    :mob_platform_weight                    SECTION ::mobs                  ;$6A47
        ;==============================================================================
PARAMS  IX`mob          ``Address of the current mob being processed
{
        set     5,[ix+#mob.flags]               ;mob does not collide with the floor
        ld      a,[ix+#mob.unknown16]
        add     a,[ix+#mob.unknown17]
        ld      [ix+#mob.unknown17],a
        cp      $18
        jr      c,._1
        ld      l,[ix+#mob.Yspeed+0]
        ld      h,[ix+#mob.Yspeed+1]
        ld      a,[ix+#mob.Ydirection]
        ld      de,$0040
        add     hl,de
        adc     a,d
        ld      [ix+#mob.Yspeed+0],l
        ld      [ix+#mob.Yspeed+1],h
        ld      [ix+#mob.Ydirection],a
._1     ld      [ix+#mob.width], 26
        ld      [ix+#mob.height],16
        
        ld      a,[$.SONIC.Ydirection]
        and     a
        jp      m,._2
        
        ld      hl,$0806
        ld      [$.TEMP6],hl
        call    :detectCollisionWithSonic
        jr      c,._2
        ld      [ix+#mob.unknown16],$01
        ld      bc,$0010
        ld      e,[ix+#mob.Yspeed+0]
        ld      d,[ix+#mob.Yspeed+1]
        call    :_LABEL_7CC1_12
._2     ld      hl,:object_platform_spriteLayouts._6911
        ld      a,[$.LEVEL_SOLIDITY]
        and     a
        jr      z,._3
        ld      hl,:object_platform_spriteLayouts._6923
._3     ld      [ix+#mob.spriteLayout+0],l
        ld      [ix+#mob.spriteLayout+1],h
        ld      hl,[$.CAMERA_Y]
        ld      de,$00c0
        add     hl,de
        ld      e,[ix+#mob.Y+0]
        ld      d,[ix+#mob.Y+1]
        and     a
        sbc     hl,de
        ret     nc
        ld      [ix+#mob.type],$FF              ;remove object?
        ret
}